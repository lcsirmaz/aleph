$ error 
'pragmat'module="error".
$ HEADER *********************************************** $
'e'internal error+T[]+>file+>lineno.
'e'corrupted obj file+T[]+>file+>lineno.
'e'Ferror+*+>msg. $ fatal error $
'a'error+*+>msg.
'a'warning+*+>msg.
'table'MESSAGE.

$ BODY ************************************************* $
'pragmat'if=compile.
'pragmat'require=("input","lexical","item").

'table'MESSAGE=(
  "too many errors, quitting" :too many errors,
  "internal error at %l/" :fatal head,
  ", program aborted" :fatal tail,
  "corrupt ice file, found at %l/" :corrupted head
).
$ messages go here $
'charfile' PRINTFILE="stdout">.
'variable'prpos=0.

'a'print char+>ch: 
   incr+prpos,put char+PRINTFILE+ch.
'a'nlcr: 
   prpos<=1,1->prpos;
   0->prpos,put char+PRINTFILE+newline.
'a'tab line+>pos:
   prpos=pos;
   prpos>pos,nlcr,:tab line;
   print char+/ /,:tab line.

$ simple output $
'a'print int+>n:
   $ print n in decimal; min int does not end with 9
   n=min int, print char+/-/,subtr+-1+n+n,print int1+n+/1/;
   n<0, print char+/-/,get abs+n+n,print int1+n+/0/;
   print int1+n+/0/.
'a'print int1+>n+>c-q-r:
   divrem+n+10+q+r,(q=0; print int1+q+/0/),
        add+r+c+r, print char+r.
'a'print form+>n: print form1+n+4.
   $ print n for 4 places
'a'print form1+>n+>m -q-r:
   m=0;
   decr+m,divrem+n+10+q+r,print form1+q+m,add+r+/0/+r,print char+r.
'a'print+T[]+>p-n:
   $ print a string
   was+T+p,put string+PRINTFILE+T+p,
     string length+T+p+n,add+prpos+n+prpos;
   +.
$ formatted printing $
'a'print pointer+>ptr:
   ptr=0,print+"[NULL]";
   was+MESSAGE+ptr,print+MESSAGE+ptr;
   was+STDARG+ptr,print+STDARG+ptr;
   was+LEXT+ptr,get tag image+ptr+ptr,print+LEXT+ptr;
   internal error+_title_+_line_.
'a'print base+>str-obuff-ptr:
   >>BUFFER->obuff,
   (was+LEXT+str,get tag image+str+str,unpack string+LEXT+str+BUFFER;
    was+STDARG+str,unpack string+STDARG+str+BUFFER;
    internal error+_title_+_line_),
   >>BUFFER->ptr,
   $ go back until / or \
   (nxt: ptr<=obuff;
         BUFFER[ptr]=///;BUFFER[ptr]=/\/;BUFFER[ptr]=/:/;
         decr+ptr,:nxt),incr+ptr,
   $ print until the first dot
   (nxt: >>BUFFER>ptr;BUFFER[ptr]=/./;
         print char+BUFFER[ptr],incr+ptr,:nxt),
   unstack to+BUFFER+obuff.
'a'print item+>item-line-source-module:
   $ print item definition as "0034/aleph (aleph)"
   find lineno+item+line+source+module,
   (line>0;decr+line,print form+line),
   (source=Squote image;
    print char+///,print base+source),
   print char+/ /,print char+/(/,print pointer+module,print char+/)/.

'a'format print+*+>fmt-optr-ptr:
   >>BUFFER->optr,unpack string+MESSAGE+fmt+BUFFER,add+optr+1+ptr,
   (nxt:>>BUFFER<ptr;
          BUFFER[ptr]!=/%/,print char+BUFFER[ptr],incr+ptr,:nxt;
          incr+ptr,(>>BUFFER<ptr,print char+/%/;
                    (=BUFFER[ptr]=
                     [/p/],(shift affix block+*,print pointer+fmt;print+"[?]"),incr+ptr,:nxt;
                     [/i/],(shift affix block+*,print item+fmt;print+"[?]"),incr+ptr,:nxt;
                     [/l/],(shift affix block+*,print form+fmt;print+"[?]"),incr+ptr,:nxt;
                     [/d/],(shift affix block+*,print int+fmt;print+"[?]"),incr+ptr,:nxt;
                     print char+BUFFER[ptr],incr+ptr,:nxt))),
   (shift affix block+*,print+"[...]";+),
   unstack to+BUFFER+optr.

$ error handling ******************************************** $
'variable'error no=0.
'constant'maximal errors=51.
'constant'message col=16.

'a'message head+>M-obj:
  nlcr,print char+M,print char+/ /,get obj file+obj,
  (obj=0;print base+obj),tab line+message col.

'a'error+*+>mess:
   message head+/E/,format print+*,nlcr,
   incr+error no,(error no<maximal errors;
                  Ferror+too many errors).
'a'warning+*+>mess:
   message head+/W/,format print+*,nlcr.

'e'Ferror+*+>mess:
   message head+/F/,format print+*,nlcr,
   clean up,exit+1.

'e'internal error+T[]+>source+>line:
   message head+/F/,format print+fatal head+line,print+T+source,
   format print+fatal tail,nlcr,clean up,exit+2.
'e'corrupted obj file+T[]+>source+>line:
   message head+/F/,format print+corrupted head+line,
   print+T+source,format print+fatal tail,nlcr,clean up,exit+1.

'root'+.

'pragmat'endif=compile.
'end'
