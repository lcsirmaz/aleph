$ HEADER ************************************************** $
'pragmat'module="target".
$ generating code $
'a'open target,close target,delete target.
   $ manipulate target files $
'a'target prelude.
   $ header -- show modules and the main file marked by * $
'a'data declaration.
   $ data declaration and rule prototypes. Define stack, table,
   $  file offsets.
'a'data initialization.
   $ stack, table, and file structure initialization routines
'a'fill table head,fill table tail,data section iii,data section iv.
   $ stack and table filling 
'a'target main.
   $ generate 'wait for' external; call module roots, and
   $  the main root
'q'is pidgin rule+>item.
   $ the external rule uses pidgin call
'a'rule declaration head+>item+>C1+>C2+>C3+>minloc+>maxloc.
$ BODY **************************************************** $
'pragmat'if=compile.
'pragmat'require=("item","data","lexical","error","rule").

$ manipulate target file ********************************** $
'charfile'TARGET="aleph.c">.
'table'MESSAGE=(
 "aleph.c"  :target file name,
 "cannot create target file %p" :cannot open target
).

$ seme externals 
'stack'(next,def)LEXT=(
   "@root"    ,(0,0):Sroot,
   "a_waitfor",(0,0):ext waitfor ).

'a'open target:
  open file+TARGET+/w/+MESSAGE+target file name;
  Ferror+cannot open target+target file name.
'a'close target:
  close file+TARGET.
'a'delete target:
  close target,unlink file+MESSAGE+target file name.  
$ print to TARGET file ==================================== $
'a'print char+>ch: put char+TARGET+ch.

'a'print repr+>item: $ print the representation $
   was+ITEM+item,
     (get item def+item+item;+),
     (=type*ITEM[item]=
      [Iconstant;Ipointer constant],+;print char+/a/,print char+/_/),
     print int+repr*ITEM[item];
   print ptr+item.
'a'print int+>n:
   $ print n in decimal; min int does not end with 9
   n=min int, print char+/-/,subtr+-1+n+n,print int1+n+/1/;
   n<0, print char+/-/,get abs+n+n,print int1+n+/0/;
   print int1+n+/0/.
'a'print int1+>n+>c-q-r:
   divrem+n+10+q+r,(q=0; print int1+q+/0/),
        add+r+c+r, print char+r.
'a'print str+T[]+>ptr:
  put string+TARGET+T+ptr.
'a'print ptr+>p:
  was+ITEM+p,tag*ITEM[p]->p,:print ptr;
  was+LEXT+p,get tag image+p+p,print str+LEXT+p;
  was+MESSAGE+p,print str+MESSAGE+p;
  +.

'a'T+t[]+*+>fmt-ptr-optr:
$ formal characters:
$   r: item, get repr (S<value)
$   n: newline
$   p: pointer (LEXT)
$   d: integer
  >>BUFFER->optr,unpack string+t+fmt+BUFFER,add+optr+1+ptr,
  (nxt:>>BUFFER<ptr;
    BUFFER[ptr]!=/%/,print char+BUFFER[ptr],incr+ptr,:nxt;
    incr+ptr,(>>BUFFER>ptr,print char+/%/;
      (=BUFFER[ptr]=
       [/r/],(shift affix block+*,print repr+fmt;print char+/?/),incr+ptr,:nxt;
       [/n/],print char+newline,incr+ptr,:nxt;
       [/d/],(shift affix block+*,print int+fmt;print char+/?/),incr+ptr,:nxt;
       [/p/],(shift affix block+*,print ptr+fmt;print char+/?/),incr+ptr,:nxt;
       print char+BUFFER[ptr],incr+ptr,:nxt))),
  (shift affix block+*,print str+"[...]";+),
  unstack to+BUFFER+optr.

$ prelude ************************************************* $
$  head of the generated c file, show module names
'a'target prelude-ptr:
  T+"/* ALEPH TARGET FILE%n% * modules:",
  <<ITEM->ptr,(nxt:ptr>>>ITEM;
   (=type*ITEM[ptr]=
    [Dtitle],T+" *%p"+tag*ITEM[ptr];
    [Dmodule],T+" %p"+tag*ITEM[ptr];
    +),next+ITEM+ptr,:nxt),
  T+"%n */%n#include ""A_lice.h""%n%n".
$ main root *********************************************** $
$  generate the 'wait for' external rule.
$     for each module, a_MROOT() generates a procedure which
$     a) checks if it was waiting for;
$     b) if yes, has been executed;
$     c) if not, executes the module's root
$     d) if just executing, aborts the programs
$    rule 'wait for' just calls all these procedures until
$     some returns true (or aborts).
$  a_ROOT() calls initialization, all module roots, and
$    the main root.
'f'is waitfor+>ptr+>w>:
  $ check if the 'wait for' external has been called
  is+w;
  (get item def+ptr+ptr,is item flag+ptr+texternal,
    tag*ITEM[ptr]=ext waitfor),1->w;+.
'a'proc waitfor-ptr-head:
  $ create the external 'wait for' rule
  T+"int a_waitfor(int F1,int F2){%n",
  <<ITEM->ptr,0->head,(nxt:ptr>>>ITEM;
  (=type*ITEM[ptr]=
   [Dmodule],tag*ITEM[ptr]->head;
   [Dtitle],0->head;
   [Irule],(head=0;Sroot!=tag*ITEM[ptr];
     T+" if(a_R%r(F1,F2)){return 1;}%n"+ptr);
   +),next+ITEM+ptr,:nxt),
   T+" return 0;%n}%n".
'a'target main-ptr-head-w:
  $ for each module create a rule which executes
  $  the module root if it was called for
  <<ITEM->ptr,0->head,0->w,(nxt:ptr>>>ITEM;
   (=type*ITEM[ptr]=
    [Dmodule],tag*ITEM[ptr]->head;
    [Dtitle],0->head;
    [Irule],is waitfor+ptr+w,(head=0;Sroot!=tag*ITEM[ptr];
       T+"a_MROOT(%r,""%p"")%n"+ptr+head);
    +),next+ITEM+ptr,:nxt),
  $ generate 'wait for' if this external is used 
  (w=0;proc waitfor),
  $ procedure a_ROOT calls initialization and the
  $  root of each module, then the main root
  T+"void a_ROOT(void){%n a_data_setup();%n",
       <<ITEM->ptr,0->head,(nxt:ptr>>>ITEM;
   (=type*ITEM[ptr]=
    [Dmodule],tag*ITEM[ptr]->head;
    [Dtitle],0->head;
    [Irule],(head=0;Sroot!=tag*ITEM[ptr];
      T+" a_R%r(0,0); /* %p */%n"+ptr+head);
    +),next+ITEM+ptr,:nxt),
  <<ITEM->ptr,0->head,(nxt:ptr>>>ITEM;
    (=type*ITEM[ptr]=
     [Dmodule],0->head;
     [Dtitle],tag*ITEM[ptr]->head;
     [Irule],(head=0;Sroot!=tag*ITEM[ptr];
      T+" %r(); /* %p */%n"+ptr+head);
    +),next+ITEM+ptr,:nxt),
  T+"}%n".
$ rule declaration head *********************************** $
$  generate forward prototype for rules with standard call
$  external rules with pidgin call have no prototypes (they
$  can be #define's)
$  static/extern int/void <ID>(parameters); where parameters:
$    (int F1,int F3,int F5,int A[3]) "A" is for out and inout args
$    (int F1,int a_A[3],int a_C,int a_V[]) if it has a vararg block

'a'rule typer+>item:
  is item flag+item+rcan fail,T+"static int %r("+item;T+"static void %r("+item.
'a'external rule typer+>item:
  is item flag+item+rcan fail,T+"extern int %p("+tag*ITEM[item];
  T+"extern void %p("+tag*ITEM[item].
'a'arg sep+>sep>:
  sep=0,1->sep;print char+/,/.
'a'out args+>out+>sep>:
  out=0;arg sep+sep,T+"int a_A[%d]"+out.
'a'rule args+>item-n-cnt-type-out-sep:
  get number of formals+item+n,0->cnt->out->sep,
  (nxt: cnt<n,get formal+item+cnt+type,incr+cnt,
     ((type=Iformal out;type=Iformal inout),incr+out,:nxt;
      type=Iformal repeat,out args+out+sep,arg sep+sep,
               T+"int a_C,int a_V[])";
      arg sep+sep,T+"int F%d"+cnt,:nxt);
   (n=0,T+"void";out args+out+sep),print char+/)/).

$ the external rule uses pidgin parameter passing
'q'is pidgin rule+>item-x:
  $ check if the string starts with a letter
  is item flag+item+texternal,
  get tag image+tag*ITEM[item]+x,string elem+LEXT+x+0+x,
    (=x=[/a/:/z/;/A/:/Z/],-;+).
$ rule declaration ----------------------------------------- $
$ formal parameters, locals, and call arguments; maybe dynamic.
$   formal  F1,F2,a_A[]; a_C, a_V[] (repeat)
$   local   L4,L5;       a_D[] (dynamic)
'a'show formals as comment+>item-n-cnt-type:
  get number of formals+item+n,0->cnt,
  (nxt:cnt>=n;
    get formal+item+cnt+type,incr+cnt,
    (=type=
     [Iformal in],    T+"+>a";
     [Iformal out],   T+"+a>";
     [Iformal inout], T+">a>";
     [Iformal table], T+"+t[]";
     [Iformal stack], T+"+[]st[]";
     [Iformal file],  T+"+""""f";
     [Iformal repeat],T+"+*"),:nxt).
'a'declare locals+>minloc+>maxloc:
  minloc=0;
  minloc<=maxloc,T+"int L%d;"+minloc,incr+minloc,:declare locals;
  +.

'a'declare callargs+>item+>C1+>C2+>C3-cnt:
  (C2=0;T+"int a_A[%d];"+C2),
  (C3=0;get repeat count+item+cnt,
        T+"int *a_D=alloca(sizeof(int)*(%d+%d*C));"+C3+cnt),
  T+"/* %d, %d, %d */%n"+C1+C2+C3.
'a'rule declaration head+>rule+>C1+>C2+>C3+>minloc+>maxloc:
  rule typer+rule,rule args+rule,T+"{ /* %p"+tag*ITEM[rule],
   show formals as comment+rule,T+" */%n",
   declare locals+minloc+maxloc,
   declare callargs+rule+C1+C2+C3.
$   rule->this rule.

$ T affix ************************************************** $
$ read and translate an affix. Lvalue and Rvalue are the same


$ data declaration ***************************************** $
$ apart from rule prototypes, variables, list, files are
$ defined.
$  variable:   static int S1099=<initial value>; /* <tag> */
$  list,file:  #define S1101 (S1100+sizeof_LIST); /* <tag> */
$ external
$  variable:   extern int XXXX;
$  constant:   extern int XXXX;
$  list:       extern void XXXX(int,int); /* initializer */
$  file:       extern void XXXX(int);     /* initializer */
$ an external constant can be a procedure call or a global
$  variable, but not a #define value.

'table'MESSAGE=(
  "sizeof_LIST"   :sizeof list,
  "sizeof_CHFILE" :sizeof chfile,
  "sizeof_DFILE"  :sizeof dfile,
  "BLOCK_TOTAL"   :block total
).

'p'external plain declaration+>item:
  $ variable or constant; prototye definition
  is item flag+item+texternal,T+"extern int %p;%n"+tag*ITEM[item].
'a'var declaration+>item:
  external plain declaration+item;
  T+"static int %r=%d; /* %p */%n"+item+adm*ITEM[item]+tag*ITEM[item].
'a'block declaration+>item+>prev+>sf:
  $ list, file declaration; this defines the offset only
  T+"#define %r ("+item,
  (prev=0,T+"0";T+"%r+%p"+prev+sf),
  T+") /* %p */%n"+tag*ITEM[item],
  (is item flag+item+texternal,
    T+"extern void %p(int,int);%n"+tag*ITEM[item];+).
'a'rule prototype+>item: $ prototype $
  is pidgin rule+item; $ no prototype $
  is item flag+item+texternal,external rule typer+item,
          rule args+item,T+";%n";
  rule typer+item,rule args+item,T+"; /* %p */%n"+tag*ITEM[item].
'a'data declaration-ptr-def-prev-sf:
  0->prev->sf,<<ITEM->ptr,(nxt:ptr>>>ITEM;
  (get item def+ptr+def,ptr!=def),next+ITEM+ptr,:nxt;
  (=type*ITEM[ptr]=
   [Iconstant],(external plain declaration+ptr;+);
   [Ivariable;Istatic var],var declaration+ptr;
   [Istack;Itable;Istatic stack],
       block declaration+ptr+prev+sf,ptr->prev,sizeof list->sf;
   [Icharfile],
       block declaration+ptr+prev+sf,ptr->prev,sizeof chfile->sf;
   [Idatafile],
       block declaration+ptr+prev+sf,ptr->prev,sizeof dfile->sf;
   [Irule], rule prototype+ptr;
   +),
   next+ITEM+ptr,:nxt),
  (prev=0,T+"#define %p 0%n"+block total;
          T+"#define %p (%r+%p)%n"+block total+prev+sf),
  T+"int a_DATA_BLOCK[%p]; /* stack, table, charfile, datafile */%n"
    +block total.

$ FILL table ********************************************** $
$  generate the fill table with all fillings. 
$   where: list id, or -1, followed by a sequence of blocks
$     n=0: last block
$     n>0: block of n items
$     n<0: repeat the previous block -n times
'a'push BUFFER+>x: (* x->BUFFER *)BUFFER.

'a'fill table head:
  T+"static int a_FILL[]={ /* fill table */%n".
'a'fill table tail:
  T+"-1}; /* fill table */%n".

'a'const item+v>:
  RR+Tconst+v;
  must+Titem+v,(get item def+v+v;+),
  (=type*ITEM[v]=
   [Iconstant;Ipointer constant],repr*ITEM[v]->v;
   internal error+_title_+_line_).
'a'fill item+size>-v:
  R+Dopen,0->size,(nxt:R+Dclose;
        incr+size,const item+v,push BUFFER+v,:nxt);
  1->size,const item+v,push BUFFER+v.
'p'fill repeater+rep>:
  R+Dstar,const item+rep.
'a'flush buffer+>size+>last+>ptr:
  incr+ptr,(size=0;T+"%d,"+size),
  (nxt:size=0;T+"%d,"+BUFFER[ptr],incr+ptr,decr+size,:nxt),
  (last=0;T+"%d,"+last),
  (nxt:last=0;T+"%d,"+BUFFER[ptr],incr+ptr,decr+last,:nxt).
'a'skip fill initializer:
  R+Dcolon,must+Titem+#,:skip fill initializer;+.
'a'fill entries-list-obuff-size-last-rep:
   must+Titem+list,must+Tconst+#, $ lineno $
    T+"%r,/* %p */%n"+list+tag*ITEM[list],>>BUFFER->obuff,0->size,
    (nxt:R+Dpoint;
         fill item+last,
           (fill repeater+rep,flush buffer+size+last+obuff,
               T+"-%d,%n"+rep,
               unstack to+BUFFER+obuff,0->size;
            add+size+last+size),
           skip fill initializer,:nxt),
    flush buffer+size+0+obuff,unstack to+BUFFER+obuff,
    T+"0,%n".
'a'skip entry:
   R+Dpoint;next symbol,:skip entry.
'a'data section iii:
   $ third pass; generate entries to the table
   (nxt:R+Dexpression,skip entry,:nxt;
        R+Dlist,skip entry,:nxt;
        R+Dfile,skip entry,:nxt;
        R+Dfill,fill entries,:nxt;
        +).
   
$ RULE generation ***************************************** $
$ generate the rules
'a'data section iv:
   (nxt:R+Dexpression,skip entry,:nxt;
        R+Dlist,skip entry,:nxt;
        R+Dfile,skip entry,:nxt;
        R+Dfill,skip entry,:nxt;
        +),
   (nxt:R+Drule,make rule,:nxt;+),
   (R+Dend;corrupted ice file+_title_+_line_).

$ DATA SETUP ********************************************** $
$  setup list and file data structures. As file area uses
$    list bounds, lists should come first. Add external list
$    and file initializations. Call list_FILL which adds
$    all fillings.
$  void a_data_setup(void){ ... }
$   setup_list(0/1,<id>,calibre,lb,ub,fill size);
$   setup_chfile(<id>,direction,<stringtable>,<stringptr>);
$   setup_dfile(<id>,direction,<stringtable>,<stringptr>,number of areas);
$      add_filearea(<id>,<table>,<hash>);
$

'a'list initialization+>kind+>item-cal-lb-up-fill:
   is item flag+item+texternal,get calibre+item+cal,
     T+" setup_external_list(%d,%r,%d,""%p"");%n"+
           kind+item+cal+tag*ITEM[item];
   get calibre+item+cal,get vupb+item+up,get vlwb+item+lb,
     get fill+item+fill,
     T+" setup_list(%d,%r,%d,%d,%d,%d); /* %p */%n"
        +kind+item+cal+lb+up+fill+tag*ITEM[item].

'a'charfile initialization+>item-dir-id-ptr:
   is item flag+item+texternal,
     T+" setup_external_charfile(%r,""%p"");%n"+item+tag*ITEM[item];
   (is item flag+item+toutfile,2->dir;0->dir),
     (is item flag+item+tinfile,incr+dir;+),
     get file data+item+id+ptr+#,
     T+" setup_charfile(%r,%d,%r,%r); /* %p */%n"
        +item+dir+id+ptr+tag*ITEM[item].
'f'count file area+>p+cnt>:
   0->cnt,(nxt:p=0;incr+cnt,get file link+p+#+#,:nxt).
'a'datafile initialization+>item-dir-id-ptr-link-cnt:
   is item flag+item+texternal,
     T+" setup_external_dfile(%r,""%p"");%n"+item+tag*ITEM[item];
   (is item flag+item+toutfile,2->dir;0->dir),
     (is item flag+item+tinfile,incr+dir;+),
     get file data+item+id+ptr+link,count file area+link+cnt,
     T+" setup_dfile(%r,%d,%r,%r,%d); /* %p */%n"+item+dir+id+ptr+cnt+tag*ITEM[item],
     (nxt:link=0;
        get file link+link+cnt+id,
        T+"   add_filearea(%r,%r,%d);%n"+item+id+cnt,:nxt).

'a'data initialization-ptr-def:
   T+"static void a_data_setup(void){%n",
   <<ITEM->ptr,(nxt:ptr>>>ITEM;
    (get item def+ptr+def,ptr!=def),next+ITEM+ptr,:nxt;
     (=type*ITEM[ptr]=
       [Itable],list initialization+0+ptr;
       [Istack;Istatic stack],list initialization+1+ptr;
       +),
    next+ITEM+ptr,:nxt),
    <<ITEM->ptr,(nxt:ptr>>>ITEM;
    (get item def+ptr+def,ptr!=def),next+ITEM+ptr,:nxt;
     (=type*ITEM[ptr]=
       [Icharfile],charfile initialization+ptr;
       [Idatafile],datafile initialization+ptr;
       +),
      next+ITEM+ptr,:nxt),
   T+" a_list_fill(a_FILL);%n}%n".

$ ROOT **************************************************** $
'root'wait for+"lexical";+.
'pragmat'endif=compile.
'end'

