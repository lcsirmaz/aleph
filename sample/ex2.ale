$ towers of hanoi, full printing of the towers 
$ when linked with -g the "backtrack" rule prints out the
$          the complete call stack

'stack'[1]A,[1]B,[1]C.
'constant'total=4.
'pragmat'macro=(print disc,full disc).

'action'tower+>size+[]from[]+[]via[]+[]to[]:
   size=0,backtrack;
   decr+size,tower+size+from+to+via,
     disc+from+to,print towers,
     tower+size+via+from+to.
'action'disc+[]from[]+[]to[]:
   (*from->to*)to, unstack+from.
'action'print towers-row:
   total->row,put char+STDOUT+newline,
   (nxt: row=0;
         decr+row,print disc+A+row, print disc+B+row,
           print disc+C+row,put char+STDOUT+newline, :nxt).
'action'print disc+st[]+>row-idx:
   add+row+<<st+idx,
     (was+st+idx,full disc+st[idx];empty disc).
'action'full disc+>n-spaces:
   subtr+total+n+spaces,print chars+spaces+/ /,
     print chars+n+/*/,print chars+1+/*/,print chars+n+/*/,
     print chars+spaces+/ /.
'action'empty disc:
   print chars+total+/ /,print chars+1+/*/,print chars+total+/ /.
'action'print chars+>n+>chr:
   n=0;put char+STDOUT+chr,decr+n,:print chars.
'root'-n:
   total->n,
     (fill: n=0; (*n->A*)A,decr+n,:fill),
     print towers,tower+total+A+B+C.

'end'
