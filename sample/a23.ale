'pragmat'title="checking binary input/output".

'stack'[3](a,b,c)T1[], [4]T2[],[=654=]T3[].
'stack'[]tmpname[]=("/tmp/aXXXXXX":tmp).

'datafile'DISC[T1;T2;T3]=>"tmp">.

'var'round=1,total=0.

'f'nextptr+t[]+>idx>:
   incr+idx,(idx>>>t,<<t->idx;+).

'action'write+t[]+>n-i-idx:
$ write 4 pointers then 7 numbers. Numbers start from 3456789
   printf+"writing round %d%n"+round,incr+round,
   3456789->i,<<t->idx,(nxt:n<=0;
   decr+n,put data+DISC+i+numerical,incr+i,
          put data+DISC+idx+pointer,nextptr+t+idx,
          put data+DISC+i+numerical,incr+i,
          put data+DISC+i+numerical,incr+i,
          put data+DISC+idx+pointer,nextptr+t+idx,
          put data+DISC+i+numerical,incr+i,
          put data+DISC+i+numerical,incr+i,
          put data+DISC+i+numerical,incr+i,
          put data+DISC+idx+pointer,nextptr+t+idx,
          put data+DISC+idx+pointer,nextptr+t+idx,
          put data+DISC+i+numerical,incr+i,
          :nxt).

'action'is same+>n1+>n2+>v+>p-dv-dp-pos:
   get data+DISC+dv+dp,incr+total,get file pos+DISC+pos,
     ((dv=v,dp=p);
      printf+"data different, total=%d,pos=%d (%d/%d) v=%d,p=%d (%d,%d)%n"
             +total+pos+n2+n1+dv+dp+v+p, exit+7);
   printf+"unexpected error or eof (%d,%d)%n"+v+p,exit+8.

'action'check+t[]+>n-i-idx:
   printf+"checking round %d%n"+round,incr+round,
   3456789->i,<<t->idx,(nxt:n<=0;
   decr+n,is same+1+n+i+numerical,incr+i,
          is same+2+n+idx+pointer,nextptr+t+idx,
          is same+3+n+i+numerical,incr+i,
          is same+4+n+i+numerical,incr+i,
          is same+5+n+idx+pointer,nextptr+t+idx,
          is same+6+n+i+numerical,incr+i,
          is same+7+n+i+numerical,incr+i,
          is same+8+n+i+numerical,incr+i,
          is same+9+n+idx+pointer,nextptr+t+idx,
          is same+10+n+idx+pointer,nextptr+t+idx,
          is same+11+n+i+numerical,incr+i,
          :nxt).

'action'allcheck-pos:
  open file+DISC+/r/+tmpname+tmp,1->round,
    check+T1+444,check+T2+567,check+T3+890,
    check+T1+1444,check+T2+1567,check+T3+1890,
    get file pos+DISC+pos,
    check+T2+2121,check+T3+3121,check+T1+3321,
    (get data+DISC+#+#,printf+"Should be EOF%n",exit+6;
     set file pos+DISC+pos,
     check+T2+2121,check+T3+3121,check+T1+3321,
     close file+DISC,get file error+DISC+pos,
     printf+"Finished with error=%d%n"+pos);
  printf+"Cannot open tmp file%n".
'action'allwrite:
  (open temp file+DISC+tmpname+tmp,
   write+T1+444,write+T2+567,write+T3+890,
   write+T1+1444,write+T2+1567,write+T3+1890,
   write+T2+2121,write+T3+3121,write+T1+3321,
   close file+DISC;
  printf+"Cannot create tmp file%n").

'root' allwrite,allcheck.

'end'
