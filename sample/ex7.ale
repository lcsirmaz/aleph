$ formula manipulation $
'variable'buff=/ /.
'stack'[=100=](ch,p)opt[]=((/+/,3),(/-/,3),(/*/,4),(///,4),(/^/,5)).

'pred'next+>n:buff=n,next symbol.
'pred'letter+l>:=buff= [/a/:/z/;/A/:/Z/],buff->l,next symbol;-.
'a'next symbol:
   get char+STDIN+buff,(buff=/ /,:next symbol;+);
   -1->buff.
'a'instructions:
   printf+"Computing inverse Polish notation%n"
          "Type a formula using letters, brackets, operators:%n"
          " +, -, *, /, and ^; empty line to quit.%n".
'a'out+>n:print char+n.
'pred'optor+>prio+op>+p>-i:
   >>opt->i,
   (nxt: <<opt>i,-;
    ch*opt[i]=buff,p*opt[i]>=prio,buff->op,
        p*opt[i]->p,next symbol;
    previous+opt+i,:nxt).
'a'operand-l:
   next+/(/,form+0,(next+/)/; printf+" ) expected%n",exit+1);
   letter+l,out+l;
   printf+" operand (a letter) expected%n",exit+2.
'a'form+>prio:
   operand,(nxt-op-p:
     optor+prio+op+p,incr+p,form+p,out+op,:nxt;
     +).
'a'inverse polish:
   buff=newline;
   form+0,out+newline,(buff=newline;printf+" extra characters%n",exit+3),
     $ next round
     out+/>/,out+/ /,next symbol,:inverse polish.

'root'instructions,out+/>/,out+/ /,next symbol,inverse polish.

'end'



